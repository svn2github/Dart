universal platform

to init
	if ImageFound("linux menu icon") then
		put "linux" into universal platform
	else if ImageFound("mac menu icon") then
		put "macosx" into universal platform
	else
		put "windows" into universal platform
	end if
	Set the ImageDoctor to Auto
end init

to isLinux
	put (@universal platform is "linux") into result
	return @result
end isLinux

to isMacOSX
	put (@universal platform is "macosx") into result
	return @result
end isMacOSX

to isWindows
	put (@universal platform is "windows") into result
	return @result
end isWindows

to activateEditor
	selectMenuItem "Navigate", "Activate Editor"
end activateEditor

to openWelcomePage
selectMenuItem "Tools", "Welcome Page"
end openWelcomePage

to nextEditor count
	repeat count times
		doKeyCmd F6
		TypeText return
		Wait 2
	end repeat
end NextEditor

to checkProblems
	Wait 2
	ClIck (600,500)
	Wait 1
	Click "problems tab"
	WaitFor 1,"empty problems"
end checkProblems

(* Assumes Chromium is the active proecss. *)
to exitChromium
	if isMacOSX()
		Click (Text:"Chromium",CaseSensitive:"yes")
		Click (Text:"Quit Chromium",CaseSensitive:"yes")
		else
		TypeText altKey,F4
	end if
end exitChromium

(* Delete the output tab. Only valid if all projects have been deleted. *)
to deleteOutput
	put ImageLocation("problems tab") + (100,0) into outLoc
	Click outLoc
	Wait 0.2
	RightClick outLoc
	Wait 0.2
	put ImageFound(Text:"Close",CaseSensitive:"yes") into out
	if out
		Click (Text:"Close",CaseSensitive:"yes")
	end if
end deleteOutput

(* Delete the project identified by the given image. *)
to deleteProject project
	Click "dart sdk"
	Wait 0.2
	put ImageLocation(project) into loc
	Click loc
	Wait 0.2
	RightClick loc (* select project and show context menu *)
	Wait 0.2
	Click(Text:"Delete",CaseSensitive:"Yes") (* delete it *)
	WaitFor 0.5, "info icon" (* wait for the Delete Resources confirmation *)
	Click(Text:"Delete project contents") (* select the option to clear disk *)
	Wait 0.2
	TypeText enter
end deleteProject

to selectMenuItem with menuName, menuItem
	put the SearchRectangle into origRect
	set the SearchRectangle to ((0,0),(1920,200))
	Click(Text:menuName,CaseSensitive:"yes",ValidCharacters:"*",IgnoreSpaces:"yes",dpi:94)
	put mouselocation() - (15,0) into UL
	put UL + (500,500) into BR
	set the SearchRectangle to (UL,BR)
	Wait 0.1
	Click(Text:menuItem,CaseSensitive:"yes",ValidCharacters:"*",IgnoreSpaces:"yes",dpi:94)
	set the SearchRectangle to origRect
end selectMenuItem

to closeDebugger
	put ImageFound("debugger tab") into bug
	if bug
		Put ImageFound("stop debug button") into res
		if res then
			Click "stop debug button"
		end if
		Click "debugger tab"
	end if
end closeDebugger

to doKeyCmd key
	if isMacOSX()
		TypeText commandKey,key
	else
		TypeText controlKey,key
	end if
end doKeyCmd

to doShiftKeyCmd key
	if isMacOSX()
		TypeText commandKey,shiftKey,key
	else
		TypeText controlKey,shiftKey,key
	end if
end doShiftKeyCmd

to closeTab
	doKeyCmd("w")
end closeTab

to closeAllTabs
	doShiftKeyCmd("w")
end closeAllTabs

to saveFile
	doKeyCmd("s")
end saveFile

to openFileDialog
	doKeyCmd("o")
end openFileDialog

to openFind
	doKeyCmd("f")
end openFind

to runApp
	doKeyCmd("r")
end runApp

to selectAll
	doKeyCmd("a")
end selectAll

to copyText
	doKeyCmd("c")
end copyText

to pasteText
	doKeyCmd("v")
end pasteText

to cutText
	doKeyCmd("x")
end cutText

to undo
	doKeyCmd("z")
end undo

to newTab
	doKeyCmd("t")
end newTab

to searchBox
	doKeyCmd("3")
end searchBox

to saveFile
	doKeyCmd("s")
end saveFile

to expandTreeNode
	TypeText enter
end expandTreeNode
